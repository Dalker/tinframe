#nop A couple of scripts from the TinTin++ website examples:
#nop - alias 'log [on|off]' to log all into dated file - now modified to allow choice of named file instead
#nop - function 'colscale{foo;bar}' gives color based on foo/bar interpreted as a %
#nop And a spinoff function:
#nop - function 'colpercent{foo;bar}' gives back foo/bar written as percentage with adequate color

#nop << log [on|off];
#alias {log} {
  #if {"%0" == "on"} {
    #if {"$logging" != "on"} {
      #format {logfile} {logs/%t.html} {%Y-%m-%d};
      #log append $logfile;
      #var logging on
    };
    #else {
      #showme You are already logging.
    };
  };
  #elseif {"%0" == "off"} {
    #if {"$logging" == "on"} {
      #log off;
      #var logging off
    };
    #else {
      #showme You are not logging.
    };
  };
  #elseif {"%0" != ""} {
    #if {"$logging" != "on"} {
      #format {logfile} {logs/%t.html} {%0};
      #log append $logfile;
      #var logging on
    };
    #else {
      #showme You are already logging.
    };
  };
  #else {
    #showme Turn logging on or off?
  };
}
#nop >>
#nop << colscale{foo;bar} and colpercent{foo;bar};
#function {colscale} {
  #switch {10 * %1 / %2} {
    #case {0} {#return <faa>};
    #case {1} {#return <fba>};
    #case {2} {#return <fca>};
    #case {3} {#return <fda>};
    #case {4} {#return <fea>};
    #case {5} {#return <ffa>};
    #case {6} {#return <efa>};
    #case {7} {#return <dfa>};
    #case {8} {#return <cfa>};
    #case {9} {#return <bfa>};
    #default  {#return <afa>}
  }
}

#function {colpercent} {
  #math temp {%1* 100 / %2};
  #return @colscale{%1;%2}$temp<099>%;
}
#nop >>

#nop #echo {class stdlib (re)loaded};
#nop ------------------------
#nop CopyLeft by dalker
#nop create date: 2014-02-12
#nop modif  date: 2015-07-04
